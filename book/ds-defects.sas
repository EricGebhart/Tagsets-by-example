/* libname defdata  server=henman.shr1 access=readonly; */
/* libname defdataa server=henman.shr1 access=readonly; */
libname defdatas server=henman.shr1 access=readonly;
filename webout 'c:\jaseco-nobackup\tmp\ds-defects.html';

%let numfuture=999.999;

data mydefs(keep=defectid cmponent num_priority defect age reptdate updtdate idlink openloc fndstage smver priority);
   set defdatas.defects;
   where (cmponent ? "DATASTEP" OR cmponent ? "DATA STEP" or suppids ? "SECOSKY" or suppids ? "KULIK") and (open = 'Y') and (cmponent ^? "XOMI");
   length smver 8 word $ 32;
   label 
      age='Age in Weeks' 
      idlink='Defect ID' 
      updtdate='Last Updated'
      openloc = 'Open Location' 
      priority = 'Priority';
   age=intck('week',reptdate,today());

   /* Uncomment next line for links to goto read only page */
   /* idlink='<A HREF="http://foghorn.unx.sas.com:5022/cgi-bin/display_defect?filename=defdatas_defects_report_full&filetype=hsql&defectid=' || left(trim(defectid)) || '">' || left(trim(defectid)) || '</A>'; */

   /* full update page */
   /* idlink='<A HREF="http://foghorn.unx.sas.com:5022/cgi-bin/display_defect?filename=defdatas_update&filetype=hsql&defectid=' || left(trim(defectid)) || '">' || left(trim(defectid)) || '</A>'; */
   /* View page */
   idlink='<A HREF="http://sww.sas.com/cgi-bin/display_defect?filename=defdatas_defects_report_full&filetype=hsql&defectid=' || left(trim(defectid)) || '">' || left(trim(defectid)) || '</A>';
   select(priority);
      when ('A') num_priority = 1;
      when ('H') num_priority = 2;
      when ('M') num_priority = 3;
      when ('L') num_priority = 4;
      otherwise  num_priority = 5;
   end;

   word = scan(openloc,1,':');
   if word =: 'FUTURE' then
      smver = &numfuture;
   else do;
      smver = input(word, ?? BEST12.);   
      if missing(smver) then do;
         smver = input(scan(openloc,1,'.') || '.' || scan(openloc,2,'.'), ?? BEST12.);
      end;
   end;
run;

proc sort data=mydefs; by defectid; run;

/* Merge in actions */
libname jperm '\\dntsrc\jaseco\Work';
data mydefs;
   label status='Action';
   label group='Group';
   merge mydefs(in=indefects) jperm.defect_status jperm.defect_groups;
   by defectid;
   if indefects;
   if missing(status) and index(openloc, 'SUGGESTION') then
      status = "SUGG";
run;
libname jperm clear;

proc sort data=mydefs out=mydefsrt;
  by smver group num_priority age;
run;

/* Count number of items in each BY group */
data cnt_by_obs;
   keep cnt;

   set work.mydefsrt;
   by smver;

   if first.smver then curcnt = 0;
   curcnt + 1;
   if last.smver then do; cnt = curcnt; output; end;
run;

options missing='-';
data _null_;
   file webout;
   if _N_ = 1 then do;
      length title $ 128;
      length stoday stime $ 8;
      retain linecnt 1;

      stoday = put( today(), date7. );
      stime = put( time(), time5. ); 
      title = 'DATA Step Defect &amp; Project List &nbsp;-&nbsp; ' || stoday || "at " || stime;
      put '<!DOCTYPE HTML PUBLIC "-//IETF//DTD HTML//EN">';
      put '<!Generated by /u/jaseco/.defects/reports/datastep-status.sas>';
      put '<!Support: jaseco - Jason Secosky>';
      put '<HTML><HEAD>';
      put '<TITLE>' title '</TITLE>';
      put '</HEAD><BODY>';
      put '<CENTER><H3>' title '</H3></CENTER>';
   end;

   set work.mydefsrt;
   by smver;

   if substr(openloc,length(openloc)) =: ';' then
      openloc = substr(openloc,1,length(openloc)-1);

   if _N_ = 1 or FIRST.smver then do;
      length lab $ 32;
      set work.cnt_by_obs;
      if smver >= &numfuture then do;
         put '<CENTER><B><FONT SIZE=+2>Future / SASWARE Ballot</FONT> ' @;
         put '<FONT SIZE=-1>(' cnt +(-1) ' '@;
         if cnt = 1 then
            put 'entry)' @;
         else
            put 'entries)' @;
         put '</FONT></B></CENTER>';
      end;
      else do;
         put '<CENTER><B><FONT SIZE=+2>Version ' smver '</FONT> ' @;
         put '<FONT SIZE=-1>(' cnt +(-1) ' '@;
         if cnt = 1 then
            put 'entry)' @;
         else
            put 'entries)' @;
         put '</FONT></B></CENTER>';
      end;
      put '<TABLE ALIGN=CENTER VALIGN=MIDDLE>';
      put '<TR>';
      lab = vlabel(idlink);         put '<TH>' lab '</TH>';
      if smver = 9.1 then do;
         lab = vlabel(group);          put '<TH>' lab '</TH>';
      end;
      lab = vlabel(priority);       put '<TH>' lab '</TH>';
      lab = vlabel(defect);         put '<TH>' lab '</TH>';
      lab = vlabel(openloc);        put '<TH>' lab '</TH>';
      lab = vlabel(age);            put '<TH>' lab '</TH>';
      lab = vlabel(status);         put '<TH>' lab '</TH>';
      put '</TR>';
   end;

   if mod(linecnt,2) then
      put '<TR ALIGN=CENTER VALIGN=MIDDLE BGCOLOR="D5E5D2">';
   else
      put '<TR ALIGN=CENTER VALIGN=MIDDLE>';

   put '<TD ALIGN=CENTER>' idlink '</TD>';
   if smver = 9.1 then do;
      put '<TD ALIGN=CENTER>' group '</TD>';
   end;
   if ( upcase(fndstage) in ('C' /* 'I' */) ) then
      put '<TD><SPAN STYLE="color=rgb(153,51,51)"><B>' priority '</B></SPAN></TD>';
   else
      put '<TD>' priority '</TD>';
   put '<TD><FONT SIZE=-1>' defect '</FONT></TD>';
   put '<TD><FONT SIZE=-1>' openloc '</FONT></TD>';
   put '<TD><FONT SIZE=-1>' age '</FONT></TD>';
   put '<TD><FONT SIZE=-1>' status '</FONT></TD>';
   put '</TR>';

   linecnt + 1;

   if LAST.smver then do;
      put '</TABLE>';
      linecnt = 1;
      put '<BR><HR><BR>';
      put '</BODY></HTML>';
   end;
run;
options missing='.';
